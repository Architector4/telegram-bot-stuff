(*
This file defines the grammar of a "Known Domain Catalog" file.

Known Domain Catalog files should use the ".kdc" file extension to identify themselves.

This defines version 1.1.0 of the grammar.
*)



known_domain_catalog = { entry_line } ;

entry_line = empty_line
           | comment_only_line
           | domain_entry_line
           ;


empty_line = line_end;

comment_only_line = line_comment, line_end ;

domain_entry_line = domain_name, [ whitespace, url ], [ whitespace, line_comment ], line_end ;

line_comment = "#", { comment_char } 
             | "#";



(* technical definitions *)

domain_name = { word_char }, [ ".", domain_name ] ;

url = ? see https://www.w3.org/Addressing/URL/5_BNF.html ? ;

line_end = "\n" | "\r\n" ;

whitespace = { inline_whitespace_char } ; 
inline_whitespace_char = ? inline whitespace characters ? ; (* newline characters are excluded. Should match /\s+/ in PCRE *)

word_char = ? word characters ? ; (* should match /\w+/ in PCRE *)

comment_char = ? all visible characters ? ; (* should match /.+/ in PCRE *)